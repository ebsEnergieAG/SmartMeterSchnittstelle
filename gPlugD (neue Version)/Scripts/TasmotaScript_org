>D
;L&G 360 ebs

upH=0
sr=0
ss=0

rBlink=0
d=750
bu=0
noWifi=0
; define pins
rL=6
gL=5
bL=7
butA=9
; shared var within local network
g:Pi=0
g:Po=0

>B
->setOption55 1
->sensor53 r
;->sensor53 l14

;configure GPIO
spinm(rL O)
spinm(gL O)
spinm(bL O)
spinm(butA P)

>BS
; lightshow at startup
spin(rL 1)
delay(d)
spin(gL 1)
; r + g = y
delay(d)
spin(rL 0)
delay(d)
spin(gL 0)
spin(bL 1)
delay(d)
spin(bL 0)

>M 1

+1,4,o,0,115200
;1,=so3,512
; substitue your 32 char HEX Key (GPK0) for "DECR-KEY" below
; or, if data is not encrypted, use "1,=so4,"
;1,=so4,DECR-KEY

1,0-0:96.1.1(@1,SM-ID,,SMid,0

1,=h<hr/>
1,=h<h4>Real Power In & Out<h4/>
1,1-0:1.7.0(@1,Pi ___ 1·7,kW,Pi,19
1,1-0:2.7.0(@1,Po ___ 2·7,kW,Po,19
1,=h<hr/>
1,=h<h4>Phase Power In & Out<h4/>
1,1-0:21.7.0(@0.001,P1i ___ 21·7,W,P1i,16
1,1-0:41.7.0(@0.001,P2i ___ 41·7,W,P2i,16
1,1-0:61.7.0(@0.001,P3i ___ 61·7,W,P3i,16
;
1,1-0:22.7.0(@0.001,P1o ___ 22·7,W,P1o,16
1,1-0:42.7.0(@0.001,P2o ___ 42·7,W,P2o,16
1,1-0:62.7.0(@0.001,P3o ___ 62·7,W,P3o,16
1,=h<hr/>
1,=h<h4>Phase Voltage & Current<h4/>
1,1-0:32.7.0(@1,V1 ___ 32·7,V,V1,16
1,1-0:52.7.0(@1,V2 ___ 52·7,V,V2,16
1,1-0:72.7.0(@1,V3 ___ 72·7,V,V3,16
;
1,1-0:31.7.0(@100,I1 ___ 31·7,A,I1,19
1,1-0:51.7.0(@100,I2 ___ 51·7,A,I2,19
1,1-0:71.7.0(@100,I3 ___ 71·7,A,I3,19
;
1,=h<hr/>
1,=h<h4>Real Energy Counters<h4/>
1,1-0:1.8.1(@1,Ei1 ___ 1·8·1,kWh,Ei1,3
1,1-0:1.8.2(@1,Ei2 ___ 1·8·2,kWh,Ei2,3
1,1-0:2.8.1(@1,Eo1 ___ 2·8·1,kWh,Eo1,3
1,1-0:2.8.2(@1,Eo2 ___ 2·8·2,kWh,Eo2,3
#

>T
; on mqtt pub, set vars
Pi=#Pi
Po=#Po

>S
; every second
sr=sunrise/60
ss=sunset/60
upH=uptime/60

; if no wifi connection after 10 sec, blink red
if wifis==0 {
  noWifi+=1
}
if noWifi>10 {
  rBlink=3
}

if pin[butA]==0 {
  bu+=1
}
if bu>2 {
  rBlink=1
  bu=0
}

>F
; every 100 ms

; blink green on (1·7) reception
if smlv[2]==1 {
  spin(gL 1)
  smlv[0]
} else {
  spin(gL 0)
}

if rBlink>0 {
  spin(rL 1)
  rBlink-=1
  noWifi=0
} else {
  spin(rL 0)
}

>W
@<style>body{font-family: monospace}h4,h2{margin: 0;color: #1fa3ec}td th{font-weight: normal;}tr th:first-child{text-align: left;}tr td:nth-child(2){text-align: right;color: lime;font-weight:normal;}table{width:100\%;}</style>
@<table><tbody><tr><th>Date \& Time</th><td>%tstamp%</td></tr><tr><th>Uptime</th><td> %1upH%h</td></tr></tbody></table>

