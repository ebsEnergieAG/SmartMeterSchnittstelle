>D
;L&G 360 ebs


rBlink=0
d=750
bu=0

noWifi=0
; define pins
rL=6
gL=5
bL=7
butA=9

; -- ARRAYS --
M:s4hp1=0 480
M:s4hp2=0 480
M:s4hp3=0 480
M:s4hpT=0 480


; -- VARS --
;4h Tabelle
avgP1=0 
avgP2=0 
avgP3=0 
avg4hCnt=0
th=0
tm=0
ts=0
tmp=0

>B
->setOption55 1
->sensor53 r

;configure GPIO
spinm(rL O)
spinm(gL O)
spinm(bL O)
spinm(butA P)

>BS
; lightshow at startup
spin(rL 1)
delay(d)
spin(gL 1)
; r + g = y
delay(d)
spin(rL 0)
delay(d)
spin(gL 0)
spin(bL 1)
delay(d)
spin(bL 0)

#daysub
wcs <script>const cty = document.getElementById('day');
wcs new Chart(cty, { type: 'line', data: { labels: [
for tmp 1 s4hpT[-1] 1   
    th=s4hpT[tmp]/3600
    tm=s4hpT[tmp]%3600/60
    ts=s4hpT[tmp]%3600%60
    wcs "%s(0(th))%:%s(0(tm))%:%s(0(ts))%",  
next
wcs ],datasets: [
wcs {label: 'L1', data: [
for tmp 1 s4hp1[-1] 1
	wcs  %s4hp1[tmp]%,
next
wcs ], borderWidth: 1 , pointRadius: 0},
wcs {label: 'L2', data: [
for tmp 1 s4hp2[-1] 1
	wcs %s4hp2[tmp]% ,
next
wcs ], borderWidth: 1 , pointRadius: 0},
wcs {label: 'L3', data: [
for tmp 1 s4hp3[-1] 1
	wcs %s4hp3[tmp]% ,
next
wcs ], borderWidth: 1, pointRadius: 0 }
wcs ]}, options: {spanGaps: false, scales: {y: {beginAtZero: true}} }});</script>


>M 1
+1,4,o,0,115200

1,=h<h3>Leistung</h3>
1,=h<hr/>
1,1-0:21.7.0(@0.001,Verbrauch L1,W,P1i,0
1,1-0:41.7.0(@0.001,Verbrauch L2,W,P2i,0
1,1-0:61.7.0(@0.001,Verbrauch L3,W,P3i,0
1,1-0:1.7.0(@1,<b>Verbrauch Total</b>,kW,Pi,2
1,=h</br>
1,1-0:22.7.0(@0.001,Abgabe L1,W,P1o,0
1,1-0:42.7.0(@0.001,Abgabe L2,W,P2o,0
1,1-0:62.7.0(@0.001,Abgabe L3,W,P3o,0
1,1-0:2.7.0(@1,<b>Abgabe Total</b>,W,Po,2
1,=h<h3>Strom & Spannung</h3>
1,=h<hr/>
1,1-0:31.7.0(@1,Strom L1,A,I1,2
1,1-0:32.7.0(@1,Spannung L1,V,U1,0
1,=h</br>
1,1-0:51.7.0(@1,Strom L2,A,I2,2
1,1-0:52.7.0(@1,Spannung L2,V,U2,0
1,=h</br>
1,1-0:71.7.0(@1,Strom L3,A,I3,2
1,1-0:72.7.0(@1,Spannung L3,V,U3,0

1,=h<h3>Energie Zähler</h3>
1,=h<hr/>
1,1-0:3.8.0(@1,R+ Energie,kWh,B1,2
1,1-0:4.8.0(@1,R- Energie,kWh,B2,2
1,1-0:1.8.0(@1,Wirk A+,kWh,E1,2
1,1-0:2.8.0(@1,Wirk A-,kWh,E2,2
1,=h<hr/>
1,=hZeitstempel
1,0-0:1.0.0(@1,YYMMDDHHmmss,,TS,0
#

>S
; if no wifi connection after 10 sec, blink red
if wifis==0 {
  noWifi+=1
}
if noWifi>10 {
  rBlink=3
}

if pin[butA]==0 {
  bu+=1
}
if bu>2 {
  rBlink=1
  bu=0
}

; every 3s
if (secs%3==0)
{
	avgP1+=sml[1]
	avgP2+=sml[2]
	avgP3+=sml[3] 
	avg4hCnt+=1
}

; every 30s
if (secs%30==0)
{
	; 4h chart. idx is set automatically
	s4hp1=avgP1/avg4hCnt
	s4hp2=avgP2/avg4hCnt
	s4hp3=avgP3/avg4hCnt
	s4hpT=((hours*3600)+(mins*60)+secs)

	avgP1=0
	avgP2=0
	avgP3=0
	avg4hCnt=0
}

>F
; every 100 ms

; blink green on (1·7) reception
if smlv[2]==1 {
  spin(gL 1)
  smlv[0]
} else {
  spin(gL 0)
}

if rBlink>0 {
  spin(rL 1)
  rBlink-=1
  noWifi=0
} else {
  spin(rL 0)
}

; WEB INTERFACE
>W
@<style>div {max-width: 1025px;width: 100vw;}  td h3{margin-block-end: 0;color: #eaeaea}th{font-weight: normal;}tr th:first-child{text-align: left;}tr td:nth-child(2){text-align: right;color: #ffe417;font-weight:normal;}table{width:100\%;}</style>
$<div><canvas id="day" style="width:vw; height:400px;"></canvas></div>
$<script src="https://cdn.jsdelivr.net/npm/chart.js"></script>
$<div>%s2hms(59170)%</div>
%=#daysub
#
